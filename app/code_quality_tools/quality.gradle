/*
  Gradle Tasks:
  https://docs.gradle.org/current/userguide/tutorial_using_tasks.html

  Checkstyle Gradle Plugin
  https://docs.gradle.org/current/userguide/checkstyle_plugin.html
 */

// Create a Gradle task to run the check.
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

check.dependsOn 'checkstyle'

// configuration directory path
def configDir = "${project.rootDir}/app"

checkstyle {
    toolVersion = 7.2
}

// Checkstyle Task
task checkstyle(type: Checkstyle) {
    description 'Check Code Standard'
    group 'verification'

    configFile file("$configDir/code_quality_tools/google-android-checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'
    exclude '/R.java'
    exclude '**/BuildConfig.java'

    classpath = files()
    ignoreFailures = false
}

// PMD Task
task pmd(type: Pmd) {
    description 'Run PMD'
    group 'verification'

    ruleSetFiles = files("$configDir/code_quality_tools/pmd.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        xml.enabled = false
        html.enabled = true
    }

    ignoreFailures = false
}

// FindBugs Task
task findbugs(type: FindBugs) {
    description 'Run FindBugs'
    group 'verification'

    classes = files("$project.buildDir/intermediates/classes")
    source 'src'
//    source = fileTree('src/main/java/')


    classpath = files()
    effort 'max'
    reportLevel = 'high'

    // Use this only if you want exclude some errors
    excludeFilter file("$configDir/code_quality_tools/findbugs-exclude.xml")

    reports {
        xml.enabled = false
        html.enabled =  true
    }

    ignoreFailures = false
}


















